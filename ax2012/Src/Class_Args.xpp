
    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public Args ensureCallerFormName(FormName formName, str _funcname = '')
    {;
        Args::assertCallerFormName(this, formName, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public Args ensureCallerType(classId  potentialAncestorClassId, str _funcname = '')
    {;
        Args::assertCallerType(this, potentialAncestorClassId, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public Args ensureEnumType(EnumId enumId, str _funcname = '')
    {;
        Args::assertEnumType(this, enumId, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public Args ensureFormDatasource(str _funcname = '')
    {;
        Args::assertFormDatasource(this, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public Args ensureRecord(TableId tableId = 0, str _funcname = '')
    {;
        Args::assertRecord(this, tableId, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public Args ensureRecordExists(TableId tableId = 0, str _funcname = '')
    {;
        Args::assertRecordExists(this, tableId, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static void assertCallerFormName(Args args, FormName formName, str _funcname = '')
    {
        if( Args::getCallerFormName(args) == formName)
            return;

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static void assertCallerType(Args args, classId  potentialAncestorClassId, str _funcname = '')
    {
        if( Args::checkCallerType(args, potentialAncestorClassId) )
            return;

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static void assertEnumType(Args args, EnumId enumId, str _funcname = '')
    {
        if( Args::checkEnumType(args, enumId) )
            return;

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static void assertFormDatasource(Args args, str _funcname = '')
    {
        if (args && args.record() && args.record().isFormDataSource())
            return;

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static void assertRecord(Args args, TableId tableId = 0, str _funcname = '')
    {
        if ( Args::checkRecord(args, tableId) )
            return;

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static void assertRecordExists(Args args, TableId tableId = 0, str _funcname = '')
    {
        if ( Args::checkRecordExists(args, tableId) )
            return;

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static boolean checkCallerType(Args args, classId  potentialAncestorClassId)
    {
        boolean checkOk = args  && args.caller() && SysDictClass::is(args.caller(), potentialAncestorClassId);

        return checkOk;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static boolean checkEnumType(Args args, EnumId enumId)
    {
        boolean checkOk = (args && args.parmEnumType() == enumId);

        return checkOk;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    // запись из указанной таблицы (или любой таблицы для tableId = 0)
    public static boolean checkRecord(Args args, TableId tableId = 0)
    {
        boolean checkOk = (args != null);

        if( checkOk && tableId )
        {
            checkOk = args.dataset() == tableId;
        }

        return checkOk;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    // запись существует (recId != 0) и запись из указанной таблицы (или любой таблицы для tableId = 0)
    public static boolean checkRecordExists(Args args, TableId tableId = 0)
    {
        boolean checkOk = (Args::checkRecord(args, tableId) && args.record() && args.record().RecId != 0);

        return checkOk;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static str getCallerFormName(Args args)
    {
        FormRun formRun;
    ;
        Args::assertCallerType(args, classnum(FormRun));

        formRun = args.caller();
        return formRun.form().name();
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    // убедиться что в аргументах enum указанного типа и
    // вернуть значение этого enum из аргументов
    // бросает исключение, если enum не указанного типа
    public static int getEnum(Args args, EnumId enumId)
    {
    ;
        Args::assertEnumType(args, enumId);
        return args.parmEnum();
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    // тоже самое, что и Args::getEnum()
    // но если в аргументах не нужный enum не выдает ошибку, а возвращает дефолтное значение.
    public static int getEnumDefault(Args args, EnumId enumId, int defaultValue)
    {
        if( Args::checkEnumType(args, enumId) )
            return args.parmEnum();

        return defaultValue;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static container getMarkedRecIds(Args args, TableId tableId)
    {
        container       ret;
        FormDataSource  fds;
        common          record;
    ;
        Args::assertRecord(args, tableId);

        fds = args.record().dataSource();
        if( fds && fds.anyMarked() )
        {
            // marked records
            for( record = fds.getFirst(true) ; record ; record = fds.getNext() )
            {
                ret += record.RecId;
            }
        }

        if( ret == connull() )
        {
            // or a record itself
            record = Args::getRecord(args, tableId);
            ret += record.RecId;
        }

        return ret;
    }

    //
    // mazzy, https://github.com/mazzy-ax/Args
    //
    public static Common getRecord(Args args, TableId tableId = 0)
    {;
        Args::assertRecord(args, tableId);

        return args.record();
    }
