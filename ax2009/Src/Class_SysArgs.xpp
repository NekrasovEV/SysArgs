
    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public SysArgs ensureCallerFormName(FormName formName, str _funcname = '')
    {;
        SysArgs::assertCallerFormName(this, formName, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public SysArgs ensureCallerType(classId  potentialAncestorClassId, str _funcname = '')
    {;
        SysArgs::assertCallerType(this, potentialAncestorClassId, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public SysArgs ensureEnum(Anytype enum, str _funcname = '')
    {;
        SysArgs::assertEnum(this, enum, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public SysArgs ensureEnumType(EnumId enumId, str _funcname = '')
    {;
        SysArgs::assertEnumType(this, enumId, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public SysArgs ensureFormDatasource(str _funcname = '')
    {;
        SysArgs::assertFormDatasource(this, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public SysArgs ensureParm(str matchPattern = '.', str _funcname = '')
    {;
        SysArgs::assertParm(this, matchPattern, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public SysArgs ensureRecord(TableId tableId = 0, str _funcname = '')
    {;
        SysArgs::assertRecord(this, tableId, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public SysArgs ensureRecordExists(TableId tableId = 0, str _funcname = '')
    {;
        SysArgs::assertRecordExists(this, tableId, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public SysArgs ensureRecordTableIds(container tableIds, str _funcname = '')
    {;
        SysArgs::assertRecordTableIds(this, tableIds, _funcname ? _funcname : funcname());
        return this;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static void assertCallerFormName(Args args, FormName formName, str _funcname = '')
    {
        if( SysArgs::getCallerFormName(args) == formName)
            return;

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static void assertCallerType(Args args, classId  potentialAncestorClassId, str _funcname = '')
    {
        if( SysArgs::checkCallerType(args, potentialAncestorClassId) )
            return;

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static void assertEnum(Args args, Anytype enum, str _funcname = '')
    {
        if ( SysArgs::checkEnum(args, enum) )
            return;

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static void assertEnumType(Args args, EnumId enumId, str _funcname = '')
    {
        if( SysArgs::checkEnumType(args, enumId) )
            return;

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static void assertFormDatasource(Args args, str _funcname = '')
    {
        if (SysArgs::checkFormDatasource(args))
            return;

        throw error(Error::wrongUseOfFunction(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static void assertParm(Args args, str matchPattern = '.', str _funcname = '')
    {
        if ( SysArgs::checkParm(args, matchPattern) )
            return;

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static void assertRecord(Args args, TableId tableId = 0, str _funcname = '')
    {
        if ( SysArgs::checkRecord(args, tableId) )
            return;

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static void assertRecordExists(Args args, TableId tableId = 0, str _funcname = '')
    {
        if ( SysArgs::checkRecordExists(args, tableId) )
            return;

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static void assertRecordTableIds(Args args, container tableIds, str _funcname = '')
    {
        if ( SysArgs::checkRecordTableIds(args, tableIds) )
            return;

        throw error(Error::missingRecord(_funcname ? _funcname : funcname()));
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static boolean checkCallerType(Args args, classId  potentialAncestorClassId)
    {
        boolean checkOk = args && args.caller() && SysDictClass::is(args.caller(), potentialAncestorClassId);
        ;

        return checkOk;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static boolean checkEnum(Args args, Anytype enum)
    {
        boolean checkOk = (args != null);

        if( checkOk && typeof(enum) == types::Enum)
        {
            checkOk = (enum == args.parmEnum());
        }

        return checkOk;
    }


    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static boolean checkEnumType(Args args, EnumId enumId)
    {
        boolean checkOk = (args && args.parmEnumType() == enumId);

        return checkOk;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static boolean checkFormDatasource(Args args)
    {
        if (args && args.dataset() && args.record().isFormDataSource())
            return true;

        return false;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    // параметр parm содержит паттерн.
    // возвращает true, если parm-строка соответствует regexp-выражению в matchPattern (метод использует функцию match для проверки)
    // если не указать matchPattern, то возвращает true, если parm не пустой (содержит хотя бы один символ)
    // возвращает true, если matchPattern является пустой строкой
    public static boolean checkParm(Args args, str matchPattern = '.')
    {
        boolean checkOk = (args && match(matchPattern, args.parm()));

        return checkOk;
    }


    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    // запись из указанной таблицы (или любой таблицы для tableId = 0)
    public static boolean checkRecord(Args args, TableId tableId = 0)
    {
        boolean checkOk = (args != null);
        int     dataset;

        if( checkOk && tableId )
        {
            dataset = args.dataset();
            checkOk = dataset == tableId;
        }

        return checkOk;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    // запись существует (recId != 0) и запись из указанной таблицы (или любой таблицы для tableId = 0)
    public static boolean checkRecordExists(Args args, TableId tableId = 0)
    {
        boolean checkOk = (SysArgs::checkRecord(args, tableId) && args.record() && args.record().RecId != 0);

        return checkOk;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    // возвращает true, если:
    // * контейнер содержит tableId, совпадающий с tableId таблицы
    // * контейнер содержит 0
    // * контейнер пустой
    // возвращает false в остальных случаях
    public static boolean checkRecordTableIds(Args args, container tableIds)
    {
        boolean checkOk = (args != null);
        int     dataset;

        if( checkOk && tableIds )
        {
            dataset = args.dataset();
            checkOk = confind(tableIds, dataset) || confind(tableIds, 0);
        }

        return checkOk;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static str getCallerFormName(Args args)
    {
        FormRun formRun;
    ;
        SysArgs::assertCallerType(args, classnum(FormRun));

        formRun = args.caller();
        return formRun.form().name();
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    // убедиться что в аргументах enum указанного типа и
    // вернуть значение этого enum из аргументов
    // бросает исключение, если enum не указанного типа
    public static int getEnum(Args args, EnumId enumId)
    {
    ;
        SysArgs::assertEnumType(args, enumId);
        return args.parmEnum();
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    // тоже самое, что и SysArgs::getEnum()
    // но если в аргументах не нужный enum не выдает ошибку, а возвращает дефолтное значение.
    public static int getEnumDefault(Args args, EnumId enumId, int defaultValue)
    {
        if( SysArgs::checkEnumType(args, enumId) )
            return args.parmEnum();

        return defaultValue;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static container getMarkedRecIds(Args args, TableId tableId)
    {
        container       ret;
        FormDataSource  fds;
        common          record;
    ;
        SysArgs::assertRecord(args, tableId);

        fds = args.record().dataSource();
        if( fds && fds.anyMarked() )
        {
            // marked records
            for( record = fds.getFirst(true) ; record ; record = fds.getNext() )
            {
                ret += record.RecId;
            }
        }

        if( ret == connull() )
        {
            // or a record itself
            record = SysArgs::getRecord(args, tableId);
            ret += record.RecId;
        }

        return ret;
    }

    //
    // mazzy, https://github.com/mazzy-ax/SysArgs
    //
    public static Common getRecord(Args args, TableId tableId = 0)
    {;
        SysArgs::assertRecord(args, tableId);

        return args.record();
    }
